CREATE PROCEDURE [dbo].[SPR_PROF_MedicalFormUpdateProSolution]
AS
BEGIN
	SET XACT_ABORT, NOCOUNT ON;

	DECLARE @AcademicYear VARCHAR(5) = (
		SELECT
			CFG.Value
		FROM ProSolutionReports.dbo.Config CFG
		WHERE
			CFG.ConfigID = 'ENR_AcademicYearID'
	)


	--Latest Medical Forms
	DROP TABLE IF EXISTS #LatestMedicalForms
	SELECT
		MED.AcademicYearID,
		MED.StudentDetailID,
		MED.StudentRef,
		HasCompletedForm = 1,
		MED.CanContactStudent18PlusNOK,
		RowNum = 
			ROW_NUMBER () OVER (
				PARTITION BY
					MED.AcademicYearID,
					MED.StudentDetailID
				ORDER BY
					MED.CreatedDate DESC
			)
		INTO #LatestMedicalForms
	FROM ProSolutionForms.dbo.MedicalLearningSupportAndTripConsent MED
	WHERE
		MED.AcademicYearID >= @AcademicYear



	--Insert Application
	DROP TABLE IF EXISTS #UpdatedStudentDetail
	CREATE TABLE #UpdatedStudentDetail (
		AcademicYearID VARCHAR(5),
		StudentDetailID INT,
		OldUserDefined46 VARCHAR(50),
		NewUserDefined46 VARCHAR(50),
		OldAcceptShareInfoConsent INT,
		NewAcceptShareInfoConsent INT
	)
	UPDATE SD
	SET
		SD.UserDefined46 = 'True',
		SD.AcceptShareInfoConsent = COALESCE ( MED.CanContactStudent18PlusNOK, 0 ),
		SD.LastModifiedBy = 'MedicalFormUpdateInfo',
		SD.LastModifiedDate = GETDATE()
	OUTPUT
		INSERTED.AcademicYearID,
		INSERTED.StudentDetailID,
		DELETED.UserDefined46,
		INSERTED.UserDefined46,
		DELETED.AcceptShareInfoConsent,
		INSERTED.AcceptShareInfoConsent
	INTO #UpdatedStudentDetail
	FROM ProSolution.dbo.StudentDetail SD
	INNER JOIN #LatestMedicalForms MED
		ON MED.StudentDetailID = SD.StudentDetailID
		AND MED.AcademicYearID = SD.AcademicYearID
		AND MED.RowNum = 1
	WHERE
		SD.AcademicYearID = MED.AcademicYearID
		AND 
			EXISTS (
				SELECT
					MedicalFormCompleted = SD.UserDefined46,
					CanContactStudent18PlusNOK = SD.AcceptShareInfoConsent
				EXCEPT
				SELECT
					MedicalFormCompleted = 'True',
					COALESCE ( MED.CanContactStudent18PlusNOK, 0 )
			)


	--Insert Audit Trail Record
	DECLARE @TableName VARCHAR(50) = 'StudentDetail'
	DECLARE @RecordIDName VARCHAR(50) = 'StudentDetailID'
	DECLARE @RecordID VARCHAR(50) = '1234'
	DECLARE @RowDescription VARCHAR(50) = 'Wilson, Robin (S000000)'
	DECLARE @FieldName VARCHAR(50) = 'AcceptShareInfoConsent'
	DECLARE @FieldOldValue VARCHAR(50) = '0'
	DECLARE @FieldNewValue VARCHAR(50) = '1'
	DECLARE @FieldOldDescription VARCHAR(50) = NULL
	DECLARE @FieldNewDescription VARCHAR(50) = NULL
	DECLARE @LastModifiedBy VARCHAR(50) = 'MedicalFormUpdateInfo'
	DECLARE @Context VARCHAR(50) = 'MedicalFormUpdateInfo'

	INSERT INTO ProSolution.dbo._CCC_AuditTrail
	(
		TableName,
		WhereClause,
		RowDescription,
		ChangeInfo,
		ChangeType,
		ModifiedBy,
		ModifiedOn,
		Context,
		ProductName,
		AuditTrailAPILinkID
	)
	SELECT
		TableName = @TableName,
		WhereClause = '[' + @RecordIDName + '] = ' + CAST ( SD.StudentDetailID AS VARCHAR(50) ),
		RowDescription = SD.Surname + ', ' + SD.FirstForename + ' (' + RTRIM ( SD.RefNo ) + ')',
		ChangeInfo = 'Record Updated: ' + 
			CASE
				WHEN COALESCE ( UPD.NewUserDefined46, '' ) <> COALESCE ( UPD.OldUserDefined46, '' ) THEN 
					CHAR(13) + CHAR(10) + 'UserDefined46: Changed from ''' + COALESCE ( UPD.OldUserDefined46, '' ) + ''' to ''' + COALESCE ( UPD.NewUserDefined46, '' ) + ''''
				ELSE '' 
			END
			+ CASE
				WHEN UPD.NewAcceptShareInfoConsent <> UPD.OldAcceptShareInfoConsent THEN 
					CHAR(13) + CHAR(10) + 'AcceptShareInfoConsent: Changed from ''' + CAST ( UPD.OldAcceptShareInfoConsent AS VARCHAR(50) ) + ''' to ''' + CAST ( UPD.NewAcceptShareInfoConsent AS VARCHAR(50) ) + ''''
				ELSE '' 
			END,
		ChangeType = 2,
		ModifiedBy = @LastModifiedBy,
		ModifiedOn = GETDATE(),
		Context = @Context,
		ProductName = 'ProSolution',
		AuditTrailAPILinkID = NULL
	FROM #UpdatedStudentDetail UPD
	INNER JOIN ProSolution.dbo.StudentDetail SD
		ON SD.StudentDetailID = UPD.StudentDetailID
		AND SD.AcademicYearID = UPD.AcademicYearID
END